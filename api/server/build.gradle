apply plugin: 'kotlin'
apply plugin: 'pegasus'

apply plugin: "application"
mainClassName = "com.prezi.fail.api.ApiPackage"
applicationName = "fail-api"

distTar {
	compression Compression.BZIP2
	extension "tar.bz2"
}

// Integration with the Prezi CI system
if (hasProperty("preziReleaseTarballBasename")) {
	distTar {
		from("${parent.projectDir}/scripts") {
			into "${preziReleaseTarballBasename}/scripts"
		}
		destinationDir new File(preziReleaseDir)
		baseName preziReleaseTarballBasename
	}
}

ext.apiProject = project(":api:data")
pegasus.main.idlOptions.addIdlItem(['com.prezi.fail.api.impl'])

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}",
			// Data definitions
			project(path: ':api:data', configuration: 'dataTemplate'),
			// Rest.Li
			"com.linkedin.pegasus:restli-server:${pegasusVersion}",
			"com.linkedin.pegasus:restli-netty-standalone:${pegasusVersion}",
			// CLI
			'commons-cli:commons-cli:1.2',
			// Logging
			'org.slf4j:slf4j-api:1.7.7',
			'ch.qos.logback:logback-classic:1.1.2',
			'org.codehaus.groovy:groovy:1.8.6', // Use groovy for logback configuration
			'org.slf4j:jcl-over-slf4j:1.7.7',
			// Shared code
			project(':common')

	restClientCompile "com.linkedin.pegasus:restli-client:${pegasusVersion}"
}




